#
#    Bragg grating Lumerical simulation flow
#    see [] for documentation
#
#    Author: Mustafa Hammood   ; mustafa@siepic.com   ; mustafa@ece.ubc.ca
#    SiEPIC Kits Ltd. 2019     ; University of British Columbia
#
#    (c)2020
#############################
# Simulate device           #
#############################

mesh_y = 2e-9;
mesh_x = 5e-9;
mesh_z = 10e-9;
num_modes = 14;

wl=linspace(wl_min,wl_max,res);
T = matrix(res,1);
R = matrix(res,1);

sim_y_span = W*4 + dW*5;
sim_z_span = thickness_device*15;

# add fine mesh region
addmesh;
set('x min',-period);
set('x max',period);
set('y',0);
set('y span',sim_y_span);
set('z',0);
set('z span',sim_z_span );
set('dx',mesh_x);
set('dy',mesh_y);
set('dz',mesh_z);

# add EME simulation region and configure
addeme;
setnamed('EME','wavelength',mean(wl));
setnamed('EME',"energy conservation",3);
setnamed('EME',"number of cell groups", 4);
setnamed('EME',"start cell group", 2);
setnamed('EME',"end cell group", 3);
setnamed('EME',"periods",N_periods);
setnamed('EME',"display cells",1);
setnamed('EME',"number of modes for all cell groups",num_modes);

setnamed('EME',"y", 0);
setnamed('EME',"y span", sim_y_span);
setnamed('EME',"z", 0);
setnamed('EME',"z span", sim_z_span );
setnamed('EME',"x min", -period);
setnamed('EME',"group spans",[period/2;period/2;period/2;period/2]);

run;

setemeanalysis("update monitors",false); # saves time
setemeanalysis("include slow diagnostics",false); # saves time
setemeanalysis("include fast diagnostics",false); # saves time
setemeanalysis("calculate group delays",false); # saves time
setemeanalysis("override wavelength",true);

for(i=1:res){
    setemeanalysis('wavelength',wl(i));
    emepropagate;
    S = getresult('EME','user s matrix');

    T(i,1) = abs(S(2,1))^2;
    R(i,1) = abs(S(1,1))^2;
}

plot(wl*1e6,10*log10(T), 10*log10(R),'wavelength (um)','dB');
legend("Transmission", "Reflection");
